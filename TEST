program gerig
    implicit none
    integer, parameter :: itmax = 100
    integer, parameter :: itsm = 1000000
    double precision, parameter :: pi = 3.14159
    integer :: it, itt, its, g
    double precision :: t, dt, ddt, r0, l0, ddrho, ddr, cfree1, cfree2, cb, n, Na
    double precision :: tsat, t1, t2, vc, rhb, ccap, gamm
    double precision :: Vtot, Vnuc, Vgrow, Vmn, dr, Surf
    double precision :: slurry, tslurry, cslurry
    double precision :: r(itmax), rho(itmax)
    double precision :: a0, b0, dc, c1, c2, c0, knuc, kgro, kab, csat, ksp
    double precision :: cfree

    !CO2 solubility =0.17g/100g H20
    ! K_{sp} Ca(OH)_2= 7.9e-6, solubility, 1.85 g/l=1.85e3g/m**3
    ! Molar mass Ca(OH)_2 74.093
    !
    ! k_sp=3.47e-3 mol^2/m^6 CaCO_3 Molar weight 100.087 g/mol
    ! Cristal density 2.710e6g/m^3

    open(unit=1, status='new', file='output')
    Na = 6.022d23
    rhb = 2.710e6 / 100.087 ! mol/m^3
    kab = 12.4 ! m^3s^-1mol^-1
    knuc = 1.d7 ! mol^(3n-3)/s
    kgro = 8.06d-9 ! mol^(-g)m^(3g+1)/s
    a0 = 0.0017d6 / (2. * 16. + 12.) ! Co2 solubility in mol/m^3
    gamm = kab * a0
    ksp = 3.47d-3 ! (mol/m^3)^2
    l0 = 1.d-8 ! m
    r0 = l0 / 2. ! m
    Vmn = 4.0 * pi * r0**3 * Na
    n = 4.2
    g = 2
    ccap = 1.85d3 / 74.093 ! mol/m^3
    slurry = 4.d3 / 74.093 ! mol/m^3
    b0 = ccap
    csat = ksp / ccap
    cslurry = slurry - b0
    tslurry = cslurry / (kab * a0 * b0)
    dt = 21000.0 * csat / (kab * a0 * b0)
    t2 = 0.d0
    c2 = 0.d0
    Surf = 0.0

    ! zero matrices
    r = 0.d0
    rho = 0.d0

    cfree2 = 0.d0

    do it = 1, itmax
        ddt = dt / itsm
        dr = 0.d0

        do its = 1, itsm
            t2 = (it - 1) * dt + its * ddt
            c1 = c2
            c2 = kab * a0 * b0 * t2
            if (c2 > cslurry) then
                c2 = cslurry + b0 * (1.0 - exp(-gamm * (t2 - tslurry)))
            end if
            dc = c2 - c1
            cfree1 = cfree2
            cfree2 = cfree1 + dc
            cfree = 0.5 * (cfree1 + cfree2)
            if (cfree > csat) then
                ddrho = ddt * knuc * (cfree - csat)**n ! nucleus number generated
                rho(it) = rho(it) + ddrho
                cfree2 = cfree2 - rhb * Vmn * ddrho
                ddr = ddt * kgro * (cfree - csat)**g
                cfree2 = cfree2 - rhb * Surf * ddr
                dr = dr + ddr
            end if
        end do

        ! grow newly created particles through dt/2
        ! assume the nuclei grow dt/2
        r(it) = r0 + dr / 2

        ! grow the other particles through dt
        Vgrow = 0.d0
        Surf = Na * rho(it) * 4.0 * pi * r0**2
        do itt = 1, it - 1
            Vgrow = Vgrow + rho(itt) * Na * 4.0 * pi * dr * (r(itt)**2 + r(itt) * dr + dr**2 / 3.0)
            r(itt) = r(itt) + dr
            Surf = Surf + Na * rho(itt) * 4.0 * pi * r(itt)**2
        end do

        ccap = cfree2 + b0
        if (c2 > cslurry) then
            ccap = cfree2 + b0 * exp(-gamm * (t2 - tslurry))
        end if
        csat = ksp / ccap

        write(1, *) it, t2, cfree
        write(1, *) 'XXXXXXXXXXXXXXXXXXXXXXXXXXX'

        do itt = 1, it
            write(1, *) rho(itt), 2 * r(itt)
        end do
    end do

    close(1)
end program gerig
